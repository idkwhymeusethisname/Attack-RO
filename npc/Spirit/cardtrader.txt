//===== Current Version: =====================================
//= 1.1 
//===== Compatible With: =====================================
//= rAthena SVN
//===== Description: =========================================
//= Exchange cards for points?.?
//============================================================

mooc,121,63,6	script	Card Trader	4_M_ZONDAOYAJI,{
	mes "[Card Trader]";
	mes "หวัดดี, "+strcharinfo(0)+"!";
	mes "มีอะไรให้ข้ารับใช้ไหม ?";
	next;
	switch(select(" > รายละเอียด: > แลกการ์ดเป็นแต้มพอยท์: > ร้านค้าใช้พอยท์ซื้อ (^0055FF"+getd(.Points$)+"^000000): > ออก")) {
	case 1:
		mes "[Card Trader]";
		mes "เจ้าเคยมีปัญหาเวลาได้";
		mes "การ์ดที่ไม่ได้ใช้ไหม ?";
		mes "ปัญหานั้นจะหมดไป";
		mes "เพียงนำมันมาให้ข้า!";
		next;
		mes "[Card Trader]";
		mes "ข้าจะให้เจ้า ^0055FF"+.Points[0]+" แต้ม"+((.Points[0] == 1)?"":"")+"^000000 ต่อใบ";
		mes "สำหรับการ์ดทั่วไป, และ";
		mes "^0055FF"+.Points[1]+" แต้ม^000000 สำหรับการ์ดบอส.";
		mes "เจ้าสามารถเอาแต้มพอยท์";
		mes "ไปซื้อของในร้านค้าของข้าได้.";
		mes "ฟังดูน่าสนใจไหม ?";
		close;
	case 2:
		mes "[Card Trader]";
		mes "กรุณาเลือกการ์ด";
		mes "ที่เจ้าต้องการแลก.";
		if (.Level) {
			mes " ";
			mes "ข้าต้องการจาก";
			mes "มอนสเตอร์ที่เลเวล";
			mes .Level+" หรือมากกว่า.";
		}
		deletearray @sold_nameid[0],getarraysize(@sold_nameid);
		callshop "card_shop",2;
		npcshopattach "card_shop";
		end;
	case 3:
		mes "[Card Trader]";
		mes "เจ้ามี ^0055FF"+getd(.Points$)+"^000000 แต้ม"+((getd(.Points$) == 1)?".":".");
		callshop "card_shop",1;
		npcshopattach "card_shop";
		end;
	case 4:
		mes "[Card Trader]";
		mes "*ง่าส์*";
		mes "แล้วมาใหม่น้า!";
		close;		
	}

OnSellItem:
	mes "การ์ดที่จะแลก:";
	mes "-----------------------------------";
	for(set .@i,0; .@i<getarraysize(@sold_nameid); set .@i,.@i+1)
		if(getiteminfo(@sold_nameid[.@i],2) == 6 && compare(getitemname(@sold_nameid[.@i]),"Card")){
			if (.Level) {
				query_sql("SELECT `LV` FROM `mob_db` WHERE `DropCardid` = "+@sold_nameid[.@i],.@lv);
				if (.@lv < .Level) {
					dispbottom getitemname(@sold_nameid[.@i])+" ต่ำกว่าเลเวลขั้นต่ำ.";
					continue;
				}
			}
			set .@card_id[getarraysize(.@card_id)], @sold_nameid[.@i];
			set .@card_amt[getarraysize(.@card_amt)], @sold_quantity[.@i];
			set .@mvp, compare(.MVP$,""+@sold_nameid[.@i]);
			mes ((.@mvp)?"  ^FF0000":"  ^777777")+@sold_quantity[.@i]+"x "+getitemname(@sold_nameid[.@i])+"^000000";
			set .@card_total, .@card_total+(@sold_quantity[.@i]*((.@mvp)?.Points[1]:.Points[0]));
		}
	deletearray @sold_nameid[0], getarraysize(@sold_nameid);
	deletearray @sold_quantity[0], getarraysize(@sold_quantity);
	if (!.@card_id) {
		mes "^777777(ไม่มีการ์ด)^000000";
		close;
	}
	mes " ";
	mes "---------- รวมทั้งหมด: ^0055FF"+.@card_total+" แต้ม.^000000 -------";
	next;
	if(select(" > ^0055FFแลกเปลี่ยน...^000000: > ^777777ยกเลิก^000000") == 2) {
		mes "[Card Trader]";
		mes "โอ้, ได้เลย...";
		close;
	}
	for(set .@i,0; .@i<getarraysize(.@card_id); set .@i,.@i+1)
		delitem .@card_id[.@i],.@card_amt[.@i];
	setd .Points$, getd(.Points$)+.@card_total;
	mes "[Card Trader]";
	mes "เรียบร้อย!";
	close;

OnBuyItem:
	for(set .@i,0; .@i<getarraysize(@bought_nameid); set .@i,.@i+1)
		for(set .@j,0; .@j<getarraysize(.Shop); set .@j,.@j+2)
			if (@bought_nameid[.@i] == .Shop[.@j]) {
				set .@cost, .@cost+(.Shop[.@j+1]*@bought_quantity[.@i]);
				break;
			}
	if (.@cost > getd(.Points$)) {
		mes "[Card Trader]";
		mes "เจ้ามีแต้มพอยท์ไม่พอ.";
	}
	else {
		mes "รายการสินค้า:";
		mes "-----------------------------------";
		for(set .@i,0; .@i<getarraysize(@bought_nameid); set .@i,.@i+1) {
			getitem @bought_nameid[.@i], @bought_quantity[.@i];
			mes "  ^777777"+@bought_quantity[.@i]+"x "+getitemname(@bought_nameid[.@i])+"^000000";
		}
		mes " ";
		mes "---------- รวมทั้งหมด: ^0055FF"+.@cost+" แต้ม.^000000 -------";
		setd .Points$, getd(.Points$)-.@cost;
	}
	deletearray @bought_nameid[0], getarraysize(@bought_nameid);
	deletearray @bought_quantity[0], getarraysize(@bought_quantity);
	close;

OnInit:
	set .Level,0;   		// เลเวลมอนสเตอร์ขั้นต่ำในการแลกการ์ด.
	set .Points$,"#Card_Points";	// ตัวแปรพอยท์.
	setarray .Shop[0],		// ร้านขายของ: <รหัสไอเท็ม>,<ราคาพอยท์>
	7539,1;
	setarray .Points[0],1,10;	// แต้มพอยท์ต่อ <การ์ดทั่วไป>,<การ์ดบอส>
	set .MVP$,			// รหัสการ์ดบอส.
	"4662,4143,4146,4135,4128,4123,4144,4236,4121,4134,4142,4132,4131,4376,4147,4148,4137,4318,27126,4276,4520,4168,4372,27162,4324,"+
	"4419,4399,4263,4342,4330,4305,4302,27151,27150,4425,4456,27081,27152,4525,4403,4374,4386,27113,4441,4408,27104,4507,4509,27106,4352,"+
	"4407,4578,4574,4580,4652,4697,4430,4576,4145,4603,4601,27164,4357,4359,4361,4363,4367,4365,4565,4563,4561,4562,4564,4560,4566,"+
	"27262,27263,4556,31023,27287,27020,27119,27294,27298,4625,4605,4606,4602,4636,4592,31026,27182";

	npcshopdelitem "card_shop",909;
	for(set .@i,0; .@i<getarraysize(.Shop); set .@i,.@i+2)
		npcshopadditem "card_shop",.Shop[.@i],.Shop[.@i+1];
	waitingroom "แลกการ์ดขยะ",0;
	end;
}
-	shop	card_shop	-1,909:-1