-	script	TMPAY	 -1,{

OnInit:

	//ตั้งค่าจำนวน CASHPOINT ที่จะได้รับ

	//จำนวน CASHPOINT สำหรับบัตร 50 บาท
	set .card_cash_amount[1],200;
	//จำนวน CASHPOINT สำหรับบัตร 90 บาท
	set .card_cash_amount[2],360;
	//จำนวน CASHPOINT สำหรับบัตร 150 บาท
	set .card_cash_amount[3],600;
	//จำนวน CASHPOINT สำหรับบัตร 300 บาท
	set .card_cash_amount[4],1300;
	//จำนวน CASHPOINT สำหรับบัตร 500 บาท
	set .card_cash_amount[5],2300;
	//จำนวน CASHPOINT สำหรับบัตร 1,000 บาท
	set .card_cash_amount[6],5000;

	//ตั้งค่า Item ID และจำนวน Item ที่จะได้รับ

	//จำนวน Item ID และจำนวน Item สำหรับบัตร 50 บาท (ตั้งค่าเป็น 0 ทั้งคู่ หากไม่ต้องการแจก Item สำหรับบัตรราคานี้)
	set .card_item_id[1],31435;
	set .card_item_amount[1],50;
	//จำนวน Item ID และจำนวน Item สำหรับบัตร 90 บาท (ตั้งค่าเป็น 0 ทั้งคู่ หากไม่ต้องการแจก Item สำหรับบัตรราคานี้)
	set .card_item_id[2],31435;
	set .card_item_amount[2],90;
	//จำนวน Item ID และจำนวน Item สำหรับบัตร 150 บาท (ตั้งค่าเป็น 0 ทั้งคู่ หากไม่ต้องการแจก Item สำหรับบัตรราคานี้)
	set .card_item_id[3],31435;
	set .card_item_amount[3],150;
	//จำนวน Item ID และจำนวน Item สำหรับบัตร 300 บาท (ตั้งค่าเป็น 0 ทั้งคู่ หากไม่ต้องการแจก Item สำหรับบัตรราคานี้)
	set .card_item_id[4],31435;
	set .card_item_amount[4],300;
	//จำนวน Item ID และจำนวน Item สำหรับบัตร 500 บาท (ตั้งค่าเป็น 0 ทั้งคู่ หากไม่ต้องการแจก Item สำหรับบัตรราคานี้)
	set .card_item_id[5],31435;
	set .card_item_amount[5],500;
	//จำนวน Item ID และจำนวน Item สำหรับบัตร 1,000 บาท (ตั้งค่าเป็น 0 ทั้งคู่ หากไม่ต้องการแจก Item สำหรับบัตรราคานี้)
	set .card_item_id[6],31435;
	set .card_item_amount[6],1000;

	//CASH BONUS (สะสมจำนวน Cash เพื่อรับ Bonus Item) - ไม่ใช่หน่วยเงินบาท

	//จำนวน CASH BONUS สะสมที่จะได้รับรางวัล
	set .cash_bonus_minimum,0;
	//Item ที่จะได้รับหากลูกค้าเติมเงินสะสมถึงจำนวนที่กำหนด (ตั้งค่าเป็น 0 ทั้งคู่ หากไม่ต้องการแจก Item)
	set .cash_bonus_item_id,0;
	set .cash_bonus_item_amount,1;

	while (1)
	{
		query_sql "SELECT `card_id`,`amount`,`user_id` FROM `truemoney` WHERE `status` = 1 ORDER BY RAND()",.card_id,.card_amount,.card_account_id;
		sleep 500;
		if (getarraysize(.card_id) > 0) {
			for (set .i,0; .i < getarraysize(.card_id); set .i,.i+1) {
				if(.card_amount[.i] > 0 && attachrid(.card_account_id[.i]) == 1)
				{
					dispbottom "[Refill Center System] คุณได้รับ " + .card_cash_amount[.card_amount[.i]] + " Cash";
					set #CASHPOINTS,(#CASHPOINTS + .card_cash_amount[.card_amount[.i]]);
					set #CASHBONUS,(#CASHBONUS + .card_cash_amount[.card_amount[.i]]);
					dispbottom "[Refill Center System] คุณมี Cash Bonus สะสม " + #CASHBONUS + " Cash";
					if(.card_item_id[.card_amount[.i]] > 0 && .card_item_amount[.card_amount[.i]] > 0)
					{
						getitem .card_item_id[.card_amount[.i]],.card_item_amount[.card_amount[.i]];
						dispbottom "[Refill Center System] คุณได้รับ Item " + getitemname(.card_item_id[.card_amount[.i]]) + " จำนวน " + .card_item_amount[.card_amount[.i]] + " eA";
					}
					if(#CASHBONUS >= .cash_bonus_minimum && .cash_bonus_item_id > 0 && .cash_bonus_item_amount > 0)
					{
						set #CASHBONUS,(#CASHBONUS - .cash_bonus_minimum);
						getitem .cash_bonus_item_id,.cash_bonus_item_amount;
						dispbottom "[Refill Center System] คุณได้รับ Bonus Item " + getitemname(.cash_bonus_item_id) + " จำนวน " + .cash_bonus_item_amount + " eA";
						dispbottom "[Refill Center System] คุณมี Cash Bonus สะสม " + #CASHBONUS + " Cash";
					}
					query_sql "UPDATE `truemoney` SET `status` = 2 WHERE `card_id` = " + .card_id[.i];
					detachrid;
				}
			}
			deletearray .card_id[0],getarraysize(.card_id);
			deletearray .card_amount[0],getarraysize(.card_amount);
			deletearray .card_account_id[0],getarraysize(.card_account_id);
		}
		sleep 5000;
	}
	end;

}