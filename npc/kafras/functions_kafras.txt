//===== eAthena Script ======================================= 
//= Kafra Functions
//===== By: ================================================== 
//= eAthena Dev Team
//= Darlskies, Darkchild, Syrus22, Lupus, kobra_k88 (2.0)
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 6.3
//===== Compatible With: ===================================== 
//= eAthena 1.0
//===== Description: =========================================
//= These functions handle save, storage, cart rental, teleport,
//= and Free Teleport/Warp/Rent Cart options for all Kafra NPCs.
//=
//= Kafra's will need a lot of work. The Welcome Message argument
//= may become obsolete, as most kafras have a slightly differing
//= Welcome message.
//===== Additional Comments: ================================= 
//= v1.1 Now using functions v2.1 Added Cart Rent for Classes: Whitesmith, Professor.
//= Replaced checkoption(x) into checkcart(0) [Lupus] v2.1b Added Fix Kafra Pass Func [Kobra_k88]
//= 2.2 Final fix of the Kafra Pass Exploit! [Lupus] a -Izlude[4] fix
//= 2.2a Minor changes to function calls.  Using agruments. Added Guild options. [kobra_k88]
//= 2.2b This version uses arrays for the teleport option. Rearranged next statements to make menu transitions smoother. [kobra_k88]
//= 2.3 Removed SAVE from Niflheim. [Lupus]
//= 2.3 removed "fix" by HawkMoon RTFM and check supernovice.txt . There's a SPECIAL Kafra which gives CARTS to SN for a special proce. [Lupus]
//= 2.4 Added Baby Class Support (Baby Novice check) Removed annoying storage feature where u had to close dialog window to be able to use your storage [Lupus]
//= 2.5 Added Louyang official Kafra, fixes some Kafras, not letting you to Save your position [Lupus]
//= 2.6 Reverted Dungeons Kafras (they should offer only Storage). Added temp Ayothaya Kafra
//= 2.7 Added correct Ayothaya, Louyang & Amatsu Kafras. [Lupus]
//= 2.8 Fixed Amatsu Storage problems [Lupus] 2.9 Fixed spelling mistakes. [Nexon]
//= 3.0 Added special "not working teleport menu" for Einbroch Kafras [Lupus]
//= 3.1 Removed callfunc Is_####_Class in favor of baseClass [Silentdragon]
//= 3.2 Fixed an exploit [Lupus]
//= 4.0 Added Kafra storage password protection. [Lupus]
//=	to block Kafra Password, read comments at 350 line
//= 5.0 Fixed the close2;....close; end; lines. Who let them slip away? =/ [erKURITA]
//=	  Also, the kafra upon warp was giving back the zeny. Removed. [erKURITA]
//= 5.1 Optimized a little, added no tele/save arg's [Evera]
//= 5.1a Temporary? Added F_ClearGarbage to clear unused/outdated variables [Lupus]
//= 5.2 By default commented out custom Guilds Kafra's "Guild Storage". [Lupus]
//= 5.3 uncommented Guild Storage. Confirmed kRO feature. [Lupus]
//= 5.4 added -Guide option (Kafra shows you nearest Guide) Work in progress. 
//=	Need all coords for all guides.. and somehow pass their coords to kafra.
//=	Removed universal Kafra Pass, added 3 new Kafra Tickets [Lupus]
//= 5.4b temporary moved "-Guide" from the 1st menu punct [Lupus]
//= 5.5 Added proper Niflheim welcome message.	[L0ne_W0lf]
//=	Cleaned up the menus a bit. Got rid of the proceeding "-" prefix.
//= 5.5b Missed a preceeding hyphen, which was screwing up teleporting. [L0ne_W0lf]
//= 5.6 Further dialog updates, added  dynamic costs for cart and storage use. [L0ne_W0lf]
//=	Updated some functions to handle Guild Kafras the way they should. 
//= 5.6a Corrected a few wrong names in the teleport function. [L0ne_W0lf]
//= 5.7 Correced end message no longer displaying, and the duplicate names [L0ne_W0lf]
//=	Thanks to Barron-Monster for pointing out the issues.
//= 5.8 Fixed another double name being shown when "saving" [L0ne_W0lf]
//= 5.9 Fixed the Kafra Welcome message for guilds. Thanks Barron-Monster. [L0ne_W0lf]
//= 6.0 Closed Kafra Password exploit in Cool Event Corp. Storages until we got a client/packet based password support.
//=		Added 2 args into F_SetKafCode to fit it in Cool Evnt Co. NPC [Lupus]
//= 6.1 Added menu for Turbo Track Kafra Staff. [L0ne_W0lf]
//= 6.2 Updated/Fixed warp cords. [Kisuka]
//= 6.3 #kafra_code is now stored as is. [brianluau]
//============================================================ 


// Main Function ===========================================================
//=   arg(0): Used to determine which welcome message to show.
//=   arg(1): Used to determine which menu to display.
//=   arg(2): Used to determine if the info menu is shown in F_KafInfo.
//=   arg(3): Cost to use Storage
//=   arg(4): Cost to Rent a Pushcart
//==========================================================================
function	script	F_Kafra	{
	callfunc "F_ClearGarbage"; //Clear outdated, unused variables

	//Display Kafra Welcome Message
	switch(getarg(0)){
		//Default message (obsolete)
		default:
		case 0:
			mes "[Kafra Employee]";
			mes "ยินดีต้อนรับสู่";
			mes "Kafra Corporation.";
			mes "บริการ Kafras";
			mes "จะอยู่เคียงข้างคุณ";
			mes "มีอะไรให้ฉันช่วย?";
			break;

		//Niflheim specific message
		case 1:
			mes "[Kafra Employee]";
			mes "^666666ข-ขอออออ-ต-ต้อนรับ";
			mes "สู่..-ค...-คราาฟราาา";
			mes "ค..-คอเปอร-รร-เรชั่น...^000000";
			break;

		//Guild Castle Kafra message
		case 2:
			set @GID,getcharid(2);
			mes "[Kafra Employee]";
	 		mes "ยินดีต้อนรับ. ^ff0000" + GetGuildName(@GID) + "^000000 จ๊ะ";
			mes "พวกเราจะอยู่เคียงข้างคุณเสมอ";
			break;

		//Amatsu specific message (obsolete)
		case 3:
			mes "[Kafra Employee]";
			mes "So, have you come from a faraway land to study our culture, or are you just sightseeing?";
			mes "In either case, why not stay awhile?";
			mes "The air is eternally heavy with the";
			mes "scent of pleasant wildflowers.";
			break;

		//Louyang and Ayothaya specific message (obsolete)
		case 4:
			mes "[Kafra Employee]";
			mes "พบกับบริการหลากหลายของ Kafra";
			mes "และบริการวาบสถานที่, คุณจะรู้สึกว่า";
			mes "อยู่ไม่ไกลจากบ้าน.";
			break;

		//NPC has it's own welcome message. (Display nothing)
		case 5:
			break;
	}
	next;

	M_Menu:
		cleararray @K_Menu0$[0],"",7;
	switch(getarg(1)){
		// Save and Storage only
		case 1:
			setarray @K_Menu0$[0],"เซฟ","เก็บของ","ยกเลิก";
			break;

		// Storage only
		case 2:
			setarray @K_Menu0$[0],"เก็บของ","ยกเลิก";
			break;

		// No Teleport (Common)
		case 3: 
			setarray @K_Menu0$[0],"เซฟ","เก็บของ","เช่ารถเข็น","ตรวจสอบข้อมูลอื่น","ยกเลิก";
			break;

		// Case 4 is Einbroch no tele message.

		// No save, or teleport. (Common)
		case 5:
			setarray @K_Menu0$[0],"เก็บของ","เช่ารถเข็น","ตรวจสอบข้อมูลอื่น","ยกเลิก";
			break;

		// Storage and Check Other Information only.
		case 6:
			setarray @K_Menu0$[0],"เก็บของ","ตรวจสอบข้อมูลอื่น","ยกเลิก";
			break;

		// Save, Storage, and Pushcart only (Kafra Warehouse)
		case 7:
			setarray @K_Menu0$[0],"เซฟ","เก็บของ","เช่ารถเข็น","ยกเลิก";
			break;

		// Save, Storage, Other Check information. (Turbo track)
		case 8:
			setarray @K_Menu0$[0],"เซฟ","เก็บของ","ตรวจสอบข้อมูลอื่น","ยกเลิก";
			break;

		// Common Kafra Employee
		default:
			setarray @K_Menu0$[0],"เซฟ","เก็บของ","ใช้บริการ Teleport","เช่ารถเข็น","ตรวจสอบข้อมูลอื่น","ยกเลิก";
			break;
	}

	// Guild Kafra Menu override (free Teleport, free Storage, Free Cart)
	if(getarg(0)==2) {
		cleararray @K_Menu0$[0],"",7;
		setarray @K_Menu0$[0],"เก็บของ","เก็บของกิล","เช่ารถเข็น","ใช้บริการ Teleport","ยกเลิก";
	}
	menu 	@K_Menu0$[0],K_Menu0,@K_Menu0$[1],K_Menu1,@K_Menu0$[2],K_Menu2,
		@K_Menu0$[3],K_Menu3,@K_Menu0$[4],K_Menu4,@K_Menu0$[5],K_Menu5,
		@K_Menu0$[6],K_Menu6,@K_Menu0$[7],K_Menu7;
	K_Menu0:
		set @num,0;
		goto K_Menuf;
	K_Menu1:
		set @num,1;
		goto K_Menuf;
	K_Menu2:
		set @num,2;
		goto K_Menuf;
	K_Menu3:
		set @num,3;
		goto K_Menuf;
	K_Menu4:
		set @num,4;
		goto K_Menuf;
	K_Menu5:
		set @num,5;
		goto K_Menuf;
	K_Menu6:
		set @num,6;
		goto K_Menuf;
	K_Menu7:
		set @num,7;
	K_Menuf:
		if (@K_Menu0$[@num] == "เซฟ") return;
		if (@K_Menu0$[@num] == "เก็บของ"){
			// Do not charge for Guild Storage
			if(getarg(0) == 2) callfunc "F_KafStor",2,0,0;
			else callfunc "F_KafStor",0,getarg(3),getarg(0);
			next;
			goto M_Menu;
		}
		if (@K_Menu0$[@num] == "ใช้บริการ Teleport"){
			// Display Einbroch "No Teleport Service" notice.
			if(getarg(1) == 4){
				mes "[Kafra Employee]";
				mes "เนื่องจาก ^FF0000ข้อจำกัดในการบริการเคลื่อนย้ายสถานที่";
				mes "ไปยังเป้าหมาย^000000, ทาง";
				mes "Kafra Corporation ไม่สามารถ";
				mes "พาท่านไปยัง";
				mes "Schwaltzvalt Republic ได้ค่ะ";
				next;
				mes "[Kafra Employee]";
				mes "เราขอให้คุณ";
				mes "ใช้บริการ Airship แทนค่ะ";
				mes "ขอขอบคุณที่เข้าใจ";
				mes "ในความหวังดีของเรา.";
				next;
				goto M_Menu;
			}
			callfunc "F_KafTele",getarg(0);
			goto M_Menu;
		}
		if (@K_Menu0$[@num] == "เช่ารถเข็น"){
			if(callfunc("F_KafCart",getarg(0),getarg(4)) == 1) next;
			goto M_Menu;
		}
		if (@K_Menu0$[@num] == "Guide"){
			callfunc "F_KafGuide";
			next;
			goto M_Menu;
		}
		if (@K_Menu0$[@num] == "ตรวจสอบข้อมูลอื่น"){
			callfunc "F_KafInfo",getarg(2);
			goto M_Menu;
		}
		if (@K_Menu0$[@num] == "ยกเลิก"){
			callfunc "F_KafEnd",getarg(0),0;
			end;
		}
		if (@K_Menu0$[@num] == "เก็บของกิล"){
			callfunc "F_KafStor",1,0;
			next;
			goto M_Menu;
		}

}


// Storage Function =======================================================
function	script	F_KafStor	{
	// Unable to access Guild Storage (Busy)
	if(getarg(0) == 1){
		if(guildopenstorage() == 1){
			mes "[Kafra Employee]";
			mes "ขออภัย มีสมาชิกท่านอื่นใช้บริการอยู่";
			mes "โปรดลองใหม่ภายหลัง";
			close2;
			cutin "", 255;
			end;
		}
		cutin "", 255;
		close;
	}

	// Unable to access Normal Storage (Insufficient Basic Skills)
	if(basicskillcheck() && getskilllv("NV_BASIC") < 6){
		mes "[Kafra Employee]";
		// Niflheim Specific Message
		if (getarg(2) == 1) {
			mes "^666666S-s-ssoooorry,";
			mes "y-you're a-a-aaaa";
			mes "Nooviiice... N-neeeds";
			mes "B-basic sssskill l-level 6...^000000";
			return;
		}
		mes "ขออภัย";
		mes "ไม่สามารถให้บริการได้";
		mes "เนื่องจากท่านต้องมีทักษะ Level6";
		mes "ในการใช้บริการเก็บของ";
		return;
	}

	// Accessing Normal Storage (Skipped if accessing Storage from Guild castle)
	if(getarg(0) != 2){
		// Consume "Free Ticket for Kafra Storage" if available.
		if(countitem(7059)) delitem 7059,1;
		else {
			if(Zeny<getarg(1)){
				mes "[Kafra Employee]";
				// Niflheim Specific Message
				if (getarg(2) == 1) {
					percentheal -50,-50;
					mes "^666666Zeeeeeny...";
					mes "M-more z-zeny...!";
					mes "N-neeed 150... zeny...";
					mes "Ergh! T-taking bl-blood~!^000000";
					return;
				}
				//Standard Message
				mes "ขออภัย";
				mes "จำนวนเงินของท่านไม่เพียงพอ";
				mes "สำหรับใช้บริการ";
				mes "ต้องการเงินจำนวน "+getarg(1)+" zeny.";
				return;
			}
			set Zeny, Zeny-getarg(1);
			set RESRVPTS, RESRVPTS + (getarg(1)/5);
		}
	}

	// Surpress the "End" message if in guild castle.
	if (getarg(0) != 2) {
		// Niflheim specific Message
		if (getarg(2) == 1) {
			percentheal 0,-10;
			mes "[Kafra Employee]";
			mes "^666666ขอบคุณ.. ที่ใช้... บริการของเราค่ะ...";
			mes "ขอบคุณ.. ที่ใช้... บริการของเราค่ะ...";
			mes "ขอบคุณ.. ที่ใช้... บริการของเราค่ะ...";
			mes "ขอบคุณ.. ที่ใช้... บริการของเราค่ะ...";
			mes "ขอบคุณ.. ที่ใช้... บริการของเราค่ะ...^000000";
		}
		// Normal message
		else {
			mes "[Kafra Employee]";
			mes "บริการเก็บของ";
			mes "นี่คือห้องเก็บของสำหรับคุณ";
			mes "ขอบคุณที่ใช้บริการ";
			mes "Kafra Service.";
		}
	}
	callfunc("F_CheckKafCode");	//check your storage password, if set

	close2;
	openstorage;
	cutin "", 255;
	end;
}


// Teleport Function ==================================================
function	script	F_KafTele	{
	mes "[Kafra Employee]";
	mes "โปรดเลือก";
	mes "จุดหมายปลายทาง";
	next;

	menu 	@wrpC$[0],M_Wrp0, @wrpC$[1],M_Wrp1, @wrpC$[2],M_Wrp2, @wrpC$[3],M_Wrp3, 
		@wrpC$[4],M_Wrp4, @wrpC$[5],M_Wrp5, @wrpC$[6],M_Wrp6;

	M_Wrp0:
		set @num, 0;
		goto L_Warp;
	M_Wrp1:
		set @num, 1;
		goto L_Warp;
	M_Wrp2:
		set @num, 2;
		goto L_Warp;
	M_Wrp3:
		set @num, 3;
		goto L_Warp;
	M_Wrp4:
		set @num, 4;
		goto L_Warp;
	M_Wrp5:
		set @num, 5;
		goto L_Warp;
	M_Wrp6:
		set @num, 6;

	L_Warp:
		if (@wrpC$[@num] == "Cancel") return;
		// Consume "Free Ticket for Kafra Transportation" if available.
		// Do not consume if in Guild castle
		if(countitem(7060) > 0 && getarg(0) != 2) delitem 7060,1;
		else {
			if (Zeny<@wrpP[@num]){
				mes "[Kafra Employee]";
				mes "ขออภัยไม่สามารถให้บริการได้";
				mes "เนื่องจากท่านมีเงินไม่เพียงพอ";
				mes "ค่าบริการในการ Teleport";
				mes "ไป "+@wrpD$[@num]+" คือ "+@wrpP[@num]+" zeny.";
				close2;
				cutin "", 255;
				end;
			}
			set Zeny, Zeny-@wrpP[@num];
			set RESRVPTS, RESRVPTS + (@wrpP[@num]/16);
		}
		if (@wrpD$[@num] == "Alberta") warp "alberta", 117, 56;
		if (@wrpD$[@num] == "Al De Baran") warp "aldebaran",168,112;
		if (@wrpD$[@num] == "Aldebaran") warp "aldebaran",168,112;
		if (@wrpD$[@num] == "Comodo") warp "comodo", 209, 143;
		if (@wrpD$[@num] == "Izlude") warp "izlude", 91, 105;
		if (@wrpD$[@num] == "Geffen") warp "geffen", 120, 39;
		if (@wrpD$[@num] == "Morroc") warp "morocc", 156, 46;
		if (@wrpD$[@num] == "Payon") warp "payon", 73, 101;
		if (@wrpD$[@num] == "Prontera") warp "prontera", 116, 72;
		if (@wrpD$[@num] == "Mjolnir Dead Pit") warp "mjolnir_02", 99, 351;
		if (@wrpD$[@num] == "Comodo Pharos Beacon") warp "cmd_fild07", 127, 134;
		if (@wrpD$[@num] == "Orc Dungeon") warp "gef_fild10", 52, 326;
		if (@wrpD$[@num] == "Umbala") warp "umbala", 100, 154;
		if (@wrpD$[@num] == "Juno") warp "yuno", 158, 125;
		if (@wrpD$[@num] == "Glast Helm") warp "glast_01", 200, 288;

		cutin "", 255;
		end;
}


// Cart Function ========================================================
function	script	F_KafCart	{
	// Ensure that the class wanting to rent a pushcart is a merchant
	if(baseClass != Job_Merchant){
		mes "[Kafra Employee]";
		mes "ขออภัย";
		mes "บริการเช่ารถเข็น";
		mes"จะใช้ได้เฉพาะกับอาชิพ,";
		mes"พ่อค้าเท่านั้น";
		
		return 1;
	}
	// Make sure the invoking character does not have a cart already
	else if(checkcart() == 1){
		mes "[Kafra Employee]";
		mes "ท่านมีรถเข็นอยู่แล้ว";
		mes "และต้องการเพิ่ม";
		mes "เราไม่สามารถให้ท่านได้";
		mes "มากกว่าหนึ่ง";
		mes "โปรดมาใหม่ภายหลัง";
		return 1;
	}

	// Consume "Free Ticket for the Cart Service" if available.
	// Do not consume if in Guild castle.
	if(countitem(7061) > 0 && getarg(0) != 2) delitem 7061,1;
	else {
		mes "[Kafra Employee]";
		mes "บริการเช่ารถเข็น";
		mes "ค่าบริการ "+getarg(1)+" zeny.";
		mes "ท่านต้องการเช่ารถเข็นใช่หรือไม่?";
		next;
		menu "Rent a Pushcart.",-, "Cancel.",M_End;
			if(Zeny<getarg(1)){
				mes "[Kafra Employee]";
				mes "ขออภัย";
				mes "จำนวนเงินของท่านไม่เพียงพอ";
				mes "ในการใช้บริการเช่ารถเข็น";
				mes "ต้องการเงินจำนวน "+getarg(1)+" zeny.";
				return 1;
			}
			set Zeny,Zeny-getarg(1);
			set RESRVPTS, RESRVPTS + 48;
	}
	setcart;
	return 1;

M_End:
	return 0;
}


// Pass Function ===============================================================
function	script	F_KafGuide	{
	mes "[Kafra Employee]";
	mes "ยังไม่มีการให้บริการในส่วนนี้";
	return;
}

// Special Reserve Points Function ===========================================
function	script	F_KafInfo	{

	sM_Menu:
//Uncomment next line to block Kafra Storage Protection
//	if(getarg(0) == 0) menu "ตรวจสอบบริการพิเศษ.",sM_ResChk, "หาตำแหน่ง Kafra",sM_KafLoc, "Cancel",sM_End;
	if(getarg(0) == 0) menu "ตรวจสอบบริการพิเศษ.",sM_ResChk, "ใส่รหัส คลังเก็บของ",sM_KafCode, "หาตำแหน่ง Kafra",sM_KafLoc, "ยกเลิก",sM_End;

	sM_ResChk:
		mes "[Kafra Employee]";
		mes "คุณ...";
		mes strcharinfo(0) + "...";
		mes "ท่านมีคะแนนสะสม";
		mes RESRVPTS+ " Special Reserve Points.";
		next;
		mes "[Kafra Employee]";
		mes "ท่านสามารถนำใบเสร็จไปแลกของรางวัล";
		mes "เล็กๆน้อยๆ";
		mes "ได้ที่เมือง Al De Baran.";
		mes "ขอบคุณที่ใช้บริการค่ะ";
		next;
		if(getarg(0) == 1) return;
		goto sM_Menu;

	sM_KafLoc:
		viewpoint 1,@viewpX[0],@viewpY[0],1,0xFF00FF;
		viewpoint 1,@viewpX[1],@viewpY[1],2,0xFF00FF;
		viewpoint 1,@viewpX[2],@viewpY[2],3,0xFF00FF;
		viewpoint 1,@viewpX[3],@viewpY[3],4,0xFF00FF;
		next;
		viewpoint 2,@viewpX[0],@viewpY[0],1,0xFF00FF;
		viewpoint 2,@viewpX[1],@viewpY[1],2,0xFF00FF;
		viewpoint 2,@viewpX[2],@viewpY[2],3,0xFF00FF;
		viewpoint 2,@viewpX[3],@viewpY[3],4,0xFF00FF;
		goto sM_Menu;

	sM_KafCode:
		callfunc("F_SetKafCode","[Kafra Employee]","Kafra Services");

	sM_End:
		return;
}


// End Function =====================================================
//  arg(0): used to determine what message to display.
//  arg(1): used to determine if save message is displayed.
//  arg(2): used to display the name of the area you're saving in.
//===================================================================
function	script	F_KafEnd	{
	mes "[Kafra Employee]";
	//Save
	if(getarg(1)==1) {
		mes "ท่านได้ทำการเซฟ";
		mes "เรียบร้อยแล้ว";
		//mes getarg(2)+".";
	}

	// Generic End
	else if(getarg(0)==0 || getarg(0)==5) {
		mes "พวกเรา Kafra Corporation,";
		mes "ขอขอบคุณทุกท่านที่ใช้บริการ  พวกเราจะบริการทุกท่านอย่างเต็มที่เพื่อความพึงพอใจสูงสุด";
	}

	// Niflheim End
	else if(getarg(0)==1) {
		percentheal 0,- 25;
		mes "^666666Kaffffra มะ-ไม่เคย";
		mes "ตาาาาาาาาาาาย. เราาาจะ.....";
		mes "อยู่....  ข-ข้าางงงง   คุณณณณ...~^000000";
	}

	close2;
	cutin "", 255;
	end;
}

// Check Storage Password Function ====================
function	script	F_CheckKafCode	{
	if(#kafra_code==0) return;
	mes " ใส่รหัสผ่าน:";
	set @code_,0;
	input @code_;
	if(@code_ != #kafra_code) {
		dispbottom " รหัสผิดพลาด";
		close2;
		cutin "",255;
		end;
	}
	set @kafcode_try,0;
	set @code_,0;
	return;
}

// Set / Change / Clear Storage Password Function ====================
// getarg(0) = NPC Name, getarg(1) = Company Name
function	script	F_SetKafCode	{
	mes getarg(0);
	if(#kafra_code) {
		mes "คลังเก็บของใส่รหัสอยู่";
		next;
		menu "เปลี่ยนรหัสผ่าน -> 5000z",-,
		"ยกเลิกรหัสผ่าน -> 1000z",M_CLEAR,
		"ยกเลิก",M_END;
	} else {
		mes ""+getarg(1)+" คุณต้องการตั้งรหัสคลังเก็บของหรือไหม:";
		
		next;
		menu "ตั้งรหัสผ่าน -> 5000z",M_SET,
		"Cancel",M_END;
	}

	mes getarg(0);
	mes "กรุณาใส่ ^0000FFรหัสเก่า^000000.";
	set @code,callfunc("F_EntKafCode");
	if(@code==0 || @code != #kafra_code) {
		mes "รหัสผ่านผิด คุณไม่สามารถกำหนดรหัสผ่านใหม่ได้.";
		goto M_END;
	}
	next;

M_SET:
	mes getarg(0);
	mes "ให้คุณใส่ ^FF0000รหัสใหม่^000000 เพื่อป้องกันคลังเก้บของ.";
	set @code,callfunc("F_EntKafCode");
	if(@code==0) {
		mes "รหัสผ่านไม่ได้ถูกเปลี่ยนแปลง.";
		goto M_END;
	}
	next;
	mes getarg(0);
	if(Zeny < 5000) goto L_ZENY;
	set Zeny,Zeny-5000;
	//set RESRVPTS, RESRVPTS + (5000/50);	//hardcoded password doesn't add pts

	set #kafra_code,@code;
	mes "คุณได้ใส่รหัสคลังเก็บของแล้ว.";
	goto M_END;	

M_CLEAR:
	mes getarg(0);
	mes "กรุณาใส่รหัสก่อนยกเลิก.";
	set @code,callfunc("F_EntKafCode");
	if(@code==0) {
		mes "รหัสผ่านไม่ได้ถูกลบออก.";
		goto M_END;
	}
	next;
	mes getarg(0);
	if(Zeny < 1000) goto L_ZENY;
	set Zeny,Zeny-1000;
	//set RESRVPTS, RESRVPTS + (1000/50);	//hardcoded password doesn't add pts
	if(@code == #kafra_code) {
		set #kafra_code,0;
		mes "ทำราการเรียบร้อบ.";
		mes "ขอบคุณที่ใช้บริการค่ะ"+getarg(1)+".";
	} else {
		mes "พาสเวิร์ดผิด ต้องเสีย 1000z.";
	}
	goto M_END;	

L_ZENY:
	mes "คุณมีเงินไม่พอ.";
M_END:
	close2;
	cutin "",255;
	end;
}

// Basic Password Validation Function ====================
function	script	F_EntKafCode	{
	mes "ระบุตัวเลขตั้งแต่ 1000~10000000:";
	set @code_,0;
	set @kafcode_try,@kafcode_try+1;
	if(@kafcode_try>10) {
		set @kafcode_try,0;
		logmes "Hack: ระหัสผ่านง่ายเกินไป";
	}
	if(input(@code_) == 1) {
		mes " ตั้งผ่านยาวเกินไป";
		return 0;
	}
	if(@code_<1000) {
		mes "คุณไม่ควรใช้รหัสที่ง่ายเกินไป.";
		return 0;
	}
	return @code_;
}

//===================================================
// Warp Points / View Points Function
//===================================================
function	script	F_KafSet	{
	deletearray @wrpC$[0], getarraysize(@wrpC$);
	deletearray @wrpD$[0], getarraysize(@wrpD$);
	deletearray @wrpP$[0], getarraysize(@wrpP$);
	deletearray @viewpX[0], getarraysize(@viewpX);
	deletearray @viewpY[0], getarraysize(@viewpY);

	.@map$ = strnpcinfo(4);
	if (.@map$ == "prontera") {
		setarray @wrpD$[0], "Izlude", "Geffen", "Payon", "Morocc", "Orc Dungeon", "Alberta";
		setarray @wrpP[0], 600, 1200, 1200, 1200, 1700, 1800;
		setarray @viewpX[0], 151, 29, 282, 152;
		setarray @viewpY[0], 29, 207, 200, 326;
	} else if (.@map$ == "alberta") {
		setarray @wrpP[0], 1200, 1800, 1800;
		setarray @wrpD$[0], "Payon", "Morocc", "Prontera";
		setarray @viewpX[0], 28, 113, 0, 0;
		setarray @viewpY[0], 229, 60, 0, 0;
	} else if (.@map$ == "aldebaran") {
		setarray @wrpP[0], 1200, 1200, 1800, 1700;
		setarray @wrpD$[0], "Geffen", "Juno" , "Izlude", "Mjolnir Dead Pit";
	} else if (.@map$ == "comodo" ) {
		setarray @wrpD$[0], "Morocc", "Comodo Pharos Beacon", "Umbala";
		setarray @wrpP[0], 1800, 1200, 1800;
	} else if (.@map$ == "cmd_fild07") {
		setarray @wrpD$[0], "Comodo", "Morocc";
		setarray @wrpP[0], 1200, 1200;
	} else if (.@map$ == "geffen") {
		setarray @wrpD$[0], "Prontera", "Al De Baran", "Orc Dungeon", "Mjolnir Dead Pit";
		setarray @wrpP[0], 1200, 1200, 1700, 1700;
		setarray @viewpX[0], 120, 203;
		setarray @viewpY[0], 62, 123;
	} else if (.@map$ == "izlude") {
		setarray @wrpD$[0], "Geffen", "Payon", "Morocc", "Al De Baran";
		setarray @wrpP[0], 1200, 1200, 1200, 1800;
	} else if (.@map$ == "morocc") {
		setarray @wrpD$[0], "Prontera", "Payon", "Alberta", "Comodo", "Comodo Pharos Beacon";
		setarray @wrpP[0], 1200, 1200, 1800, 1800, 1200;
		setarray @viewpX[0], 156, 163, 28, 292;
		setarray @viewpY[0], 97, 260, 167, 211;
	} else if (.@map$ == "umbala") {
		setarray @wrpD$[0], "Comodo";
		setarray @wrpP[0], 1800;
	} else if (.@map$ == "payon") {
		setarray @wrpD$[0], "Prontera", "Alberta", "Morocc";
		setarray @wrpP[0], 1200, 1200, 1200;
	} else if (.@map$ == "yuno") {
		setarray @wrpD$[0], "Al De Baran";
		setarray @wrpP[0], 1200;
		setarray @viewpX[0], 328, 278, 153, 0;
		setarray @viewpY[0], 108, 221, 187, 0;
	} else if (.@map$ == "job3_rune01") {
		setarray @wrpD$[0], "Izlude", "Geffen", "Payon", "Morocc", "Alberta";
		setarray @wrpP[0], 600, 1200, 1200, 1200, 1800;
	}

	.@warp_size = getarraysize(@wrpD$);

	// VIP pricing varies across the regional servers; this is most practical.
	if (VIP_SCRIPT && !vip_status(VIP_STATUS_ACTIVE)) {
		for ( .@i = 0; .@i < .@warp_size; .@i++ )
			@wrpP[.@i] *= 2;
	}

	for ( .@i = 0; .@i < .@warp_size; .@i++ )
		@wrpC$[.@i] = @wrpD$[.@i] + " -> " + @wrpP[.@i] + " z";
	@wrpC$[ .@warp_size ] = "Cancel";
	return;
}
